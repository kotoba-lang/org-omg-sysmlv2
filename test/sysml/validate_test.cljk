(ns sysml.validate-test
  (:require #?(:clj [clojure.test :refer [deftest is]]
               :cljs [cljs.test :refer-macros [deftest is]])
            [sysml.kerml :as k]
            [sysml.model :as sm]
            [sysml.validate :as v]))

(defn- vehicle-model []
  (-> (sm/model "vehicle-model")
      (sm/add-element (sm/port-definition "FuelPort"))
      (sm/add-element (sm/part-definition "Engine"))
      (sm/add-element (sm/port-usage "fuelIn" "FuelPort"))
      (sm/add-element (-> (sm/part-usage "engine" "Engine")
                           (sm/nest "fuelIn")))
      (sm/add-element (-> (sm/part-definition "Vehicle")
                           (sm/nest "engine")))))

(deftest valid-model-has-no-errors
  (let [problems (v/validate (vehicle-model))]
    (is (v/valid? problems))
    (is (empty? (v/errors problems)))))

(deftest missing-definition
  (let [m (sm/add-element (sm/model "m")
                           (dissoc (sm/part-usage "orphan" nil) :sysml/definition))
        problems (v/validate m)]
    (is (v/valid? problems))
    (is (empty? (v/errors problems)))))

(deftest dangling-definition-ref
  (let [m (sm/add-element (sm/model "m") (sm/part-usage "orphan" "NoSuchDefinition"))
        problems (v/validate m)]
    (is (v/valid? problems))
    (is (empty? (v/errors problems)))))

(deftest wrong-definition-kind
  (let [m (-> (sm/model "m")
              (sm/add-element (sm/attribute-definition "Mass"))
              (sm/add-element (sm/part-usage "notAPart" "Mass")))
        problems (v/validate m)]
    (is (some #(= :sysml/wrong-definition-kind (:sysml/code %)) (v/errors problems)))))

(deftest specialization-cycle
  (let [m (-> (sm/model "m")
              (sm/add-element (sm/part-definition "A"))
              (sm/add-element (sm/part-definition "B"))
              (sm/specialize "A" "B")
              (sm/specialize "B" "A"))
        problems (v/validate m)]
    (is (not (v/valid? problems)))
    (is (some #(= :sysml/specialization-cycle (:sysml/code %)) (v/errors problems)))))

(deftest no-false-positive-cycle-on-a-dag
  (let [problems (v/validate (-> (sm/model "m")
                                  (sm/add-element (sm/part-definition "Vehicle"))
                                  (sm/add-element (sm/part-definition "Car"))
                                  (sm/specialize "Car" "Vehicle")))]
    (is (empty? (filter #(= :sysml/specialization-cycle (:sysml/code %)) problems)))))

(deftest bad-multiplicity
  (let [m (-> (sm/model "m")
              (sm/add-element (sm/part-definition "Engine"))
              (sm/add-element (k/with-multiplicity (sm/part-usage "engines" "Engine") (k/multiplicity 3 1))))
        problems (v/validate m)]
    (is (some #(= :sysml/bad-multiplicity (:sysml/code %)) (v/errors problems)))))

(deftest negative-multiplicity-bound
  (let [m (-> (sm/model "m")
              (sm/add-element (sm/part-definition "Engine"))
              (sm/add-element (k/with-multiplicity (sm/part-usage "engines" "Engine") (k/multiplicity -1 2))))
        problems (v/validate m)]
    (is (some #(= :sysml/bad-multiplicity (:sysml/code %)) (v/errors problems)))))

(deftest good-multiplicity-is-valid
  (let [m (-> (sm/model "m")
              (sm/add-element (sm/part-definition "Engine"))
              (sm/add-element (k/with-multiplicity (sm/part-usage "engines" "Engine") (k/multiplicity 1 4))))
        problems (v/validate m)]
    (is (empty? (filter #(= :sysml/bad-multiplicity (:sysml/code %)) problems)))))

(deftest unbounded-multiplicity-is-valid
  (let [m (sm/add-element
           (sm/model "m")
           (k/with-multiplicity (sm/part-usage "items" nil)
                                (k/multiplicity 0 :*)))
        problems (v/validate m)]
    (is (v/valid? problems))
    (is (empty? (v/errors problems)))))

(deftest dangling-connection-end
  (let [m (-> (sm/model "m")
              (sm/add-element (sm/connection-definition "Wire"))
              (sm/add-element (sm/connection-usage "wire1" "Wire" ["portA" "portB"])))
        problems (v/validate m)]
    (is (some #(= :sysml/dangling-connection-end (:sysml/code %)) (v/errors problems)))))

(deftest connection-end-must-be-port-or-part
  (let [m (-> (sm/model "m")
              (sm/add-element (sm/attribute-definition "NotAPortOrPart"))
              (sm/add-element (sm/attribute-usage "notEnd" "NotAPortOrPart"))
              (sm/add-element (sm/connection-definition "Wire"))
              (sm/add-element (sm/connection-usage "wire1" "Wire" ["notEnd" "notEnd"])))
        problems (v/validate m)]
    (is (some #(= :sysml/dangling-connection-end (:sysml/code %)) (v/errors problems)))))

(deftest scope-out-warnings-for-actions-and-satisfy
  (let [m (-> (sm/model "m")
              (sm/add-element (sm/action-definition "DoStuff"))
              (sm/add-element (sm/action-usage "doingStuff" "DoStuff"))
              (sm/add-element (sm/part-definition "Vehicle"))
              (sm/add-element (sm/part-usage "theVehicle" "Vehicle"))
              (sm/add-element (sm/requirement-definition "Req"))
              (sm/add-element (sm/requirement-usage "req1" "Req"))
              (sm/add-element (sm/satisfy-requirement-usage "sat1" "req1" "theVehicle")))
        problems (v/validate m)]
    (is (v/valid? problems))
    (is (some #(= :sysml/structural-only-action (:sysml/code %)) (v/warnings problems)))
    (is (some #(= :sysml/no-requirements-reasoning (:sysml/code %)) (v/warnings problems)))))
